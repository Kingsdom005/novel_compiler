S0 -> translation_unit
translation_unit -> external_declaration
translation_unit -> translation_unit external_declaration
external_declaration -> function_definition
external_declaration -> declaration
declaration -> type_specifier ;
declaration -> type_specifier id_list ;
type_specifier -> void
type_specifier -> char
type_specifier -> short
type_specifier -> int
type_specifier -> long
type_specifier -> float
type_specifier -> signed
type_specifier -> double
type_specifier -> unsigned
id_list -> id
id_list -> id_list , id

function_definition -> type_specifier function_name parameter compound_statement
function_name -> id
parameter ->  ( )
parameter ->  ( parameter_list )
parameter_list -> parameter_list , parameter_declaration
parameter_list -> parameter_declaration
parameter_declaration -> type_specifier id

compound_statement -> { }
compound_statement -> { decalration_list }
compound_statement -> { statement_list }
compound_statement -> { decalration_list statement_list }

decalration_list -> declaration
decalration_list -> decalration_list declaration

statement_list -> statement
statement_list -> statement_list statement
statement -> compound_statement
statement -> expression_statement
statement -> selection_statement
statement -> iteration_statement
statement -> jump_statement

expression_statement -> ;
expression_statement -> expression ;
expression -> assignment_expression
assignment_expression -> primary_expression = assignment_expression
assignment_expression -> logical_or_expression
logical_or_expression -> logical_or_expression || logical_and_expression
logical_or_expression -> logical_and_expression
logical_and_expression -> logical_and_expression && equality_expression
logical_and_expression -> equality_expression
equality_expression -> equality_expression == relational_expression
equality_expression -> relational_expression
relational_expression -> relational_expression < additive_expression
relational_expression -> relational_expression > additive_expression
relational_expression -> relational_expression <= additive_expression
relational_expression -> relational_expression >= additive_expression
relational_expression -> additive_expression
additive_expression -> additive_expression - multiplicative_expression
additive_expression -> additive_expression + multiplicative_expression
additive_expression -> multiplicative_expression
multiplicative_expression -> multiplicative_expression * primary_expression
multiplicative_expression -> multiplicative_expression / primary_expression
multiplicative_expression -> multiplicative_expression % primary_expression
multiplicative_expression -> primary_expression
primary_expression -> id
primary_expression -> double_num 
primary_expression -> int_num
primary_expression -> ( expression )
selection_statement -> if ( expression ) statement
selection_statement -> if ( expression ) statement else statement
iteration_statement -> while ( expression ) statement
iteration_statement -> for ( expression_statement expression_statement ) statement
iteration_statement -> for ( expression_statement expression_statement expression ) statement
jump_statement -> continue ;
jump_statement -> break ;
jump_statement -> return ;
jump_statement -> return expression ;
